import {fine_grained} from '../query/lang.js'
import {query} from '../middleware/local-query.js'
import {memory, cpu} from '../stores/status.js'
import {bool, toggle, disable, init} from '../stores/simple'
import {icon} from '../util/icon.khufu'
import {donut} from '../util/donut.khufu'

style:
  .sample
    display: inline-block
    width: 1em
    height: 1em
  .right
    text-align: right
  .button
    cursor: pointer
    text-align: center
    border-top: solid rgb(203, 203, 203) 1px

view memtable(mem):
  <table.pure-table>
    store @open = bool | init(false)
    <thead>
      <tr>
        <th>
        <th>
          "Title"
        <th>
          "MiB"
    <tbody>
      for item of mem.items key item.title:
        if @open or not item.collapsed:
          <tr>
            <td>
              if item.color:
                <span.sample style=`background-color: ${item.color}`>
            <td>`${item.title}`
            <td.right>
              item.text
    <tfoot>
      <tr>
        <td.button colspan=3>
          link {click} toggle(@open) -> @open
          if @open:
            icon('arrow-up')
          else:
            icon('arrow-down')

view main():
  <h2>
    "Memory"
  <div.pure-g>
    store @mem = memory
      | query(fine_grained().matching('metric', '^memory\\.').tip())
    <div.pure-u-1-2.center>
      if @mem:
        donut(@mem.items, 256, 256, @mem.total)
    <div.pure-u-1-2>
      if @mem:
        memtable(@mem)
      else:
        "Loading ..."

    store @cpu = cpu
      | query(fine_grained().matching('metric', '^cpu\\.').history())

